#include "RobotArmIK.h"

#include <Wire.h>
#include <Adafruit_PWMServoDriver.h>
#include "RobotArmIK.h" // 수정된 라이브러리 호출

// --- 설정값 ---
// 서보 드라이버 채널 번호 (0부터 15까지)
#define SHOULDER_CHANNEL 0
#define UPPER_ARM_CHANNEL 1
#define LOWER_ARM_CHANNEL 2
#define GRIP_CHANNEL 3

#define CHESS_BOARD_SIZE 440.0       // 체스판 한 변 길이 (mm)
#define BOARD_MARGIN 20.0            // 체스판 한쪽 모서리 마진 (mm)
#define EFFECTIVE_BOARD_SIZE (CHESS_BOARD_SIZE - 2 * BOARD_MARGIN) // 실제 체스판 크기 (160mm)
#define SQUARE_SIZE (EFFECTIVE_BOARD_SIZE / 8.0) // 한 칸의 크기 (20mm)
#define ROBOT_ARM_OFFSET 40.0        // 로봇팔 중심과 체스판 시작점 사이의 거리 (mm)
#define DEAD_ZONE 250.0              // 잡은 말을 놓는 구역의 좌표
#define GRIP_OPEN_HEIGHT 65.0         // 그리퍼 열림 각도
#define Z_HEIGHT 30.0                // 말을 잡거나 놓을 때의 Z축 높이

// 체스판 회전 보정 (체스판이 로봇에 대해 회전되어 있을 경우)
// 0: 회전 없음, 90: 시계방향 90도, 180: 180도, 270: 반시계방향 90도
#define BOARD_ROTATION 180


//실제 체스판에서 동작시 보정값
// 1. x값 - 행 올라갈수록 동쪽 쏠림
// 2. z값 - 행 올라갈수록 아래 쏠림 
// 3-1. y값 - 행 올라갈수록 북쪽 쏠림
// 3-2. y값 - 열 바깥쪽(d-a, e-h)일수록 북쪽 쏠림

#define Z_COMP_PER_ROW  6.0  // 행 하나 올라갈 때 z를 올리는 값 (mm)
#define Z_COMP_MAX      60.0  // 최대 보정량 (안전 제한)

#define X_COMP_PER_COL   0.8   // 열 하나 증가할 때 x 보정량 (mm)
#define Y_COMP_PER_ROW   0.4   // 행 하나 증가할 때 y 보정량 (mm)

#define X_COMP_MAX       8.0   // x 최대 보정
#define Y_COMP_MAX       10.0  // y 최대 보정

#define Y_CENTER_COMP_PER_COL  0.0   // 중앙에서 한 열 멀어질 때 y 보정 (mm)
#define Y_CENTER_COMP_MAX      6.0   // 최대 보정량

// 링크 길이 (mm)
const float L1 = 300.0;
const float L2 = 365.0;

// 서보별 MIN/MAX 값
int servoMins[NUM_SERVOS] = {85, 85, 75, 210}; // 각 서보 최소 펄스
int servoMaxs[NUM_SERVOS] = {450, 450, 445, 230}; // 각 서보 최대 펄스

// 각 칸의 좌표 (로봇팔 구동부 기준, 0,0이 원점, 각 칸의 중점)
float X[8] = {
    -(EFFECTIVE_BOARD_SIZE / 2) + SQUARE_SIZE / 2,
    -(EFFECTIVE_BOARD_SIZE / 2) + 3 * SQUARE_SIZE / 2,
    -(EFFECTIVE_BOARD_SIZE / 2) + 5 * SQUARE_SIZE / 2,
    -(EFFECTIVE_BOARD_SIZE / 2) + 7 * SQUARE_SIZE / 2,
    (EFFECTIVE_BOARD_SIZE / 2) - 7 * SQUARE_SIZE / 2,
    (EFFECTIVE_BOARD_SIZE / 2) - 5 * SQUARE_SIZE / 2,
    (EFFECTIVE_BOARD_SIZE / 2) - 3 * SQUARE_SIZE / 2,
    (EFFECTIVE_BOARD_SIZE / 2) - SQUARE_SIZE / 2,
};

float Y[8] = {
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2,                   // 1행: 50mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + SQUARE_SIZE,     // 2행: 70mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 2 * SQUARE_SIZE, // 3행: 90mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 3 * SQUARE_SIZE, // 4행: 110mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 4 * SQUARE_SIZE, // 5행: 130mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 5 * SQUARE_SIZE, // 6행: 150mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 6 * SQUARE_SIZE, // 7행: 170mm (중점)
    ROBOT_ARM_OFFSET + BOARD_MARGIN + SQUARE_SIZE / 2 + 7 * SQUARE_SIZE  // 8행: 190mm (중점)
};

// 체스 표기법 배열
// 주의: 변수명과 실제 의미가 다름!
// rows는 실제로 files (a-h, 열)
// columns는 실제로 ranks (1-8, 행)
char files[8] = {'a', 'b', 'c', 'd', 'e', 'f', 'g', 'h'};  // 파일 (열)
char ranks[8] = {'1', '2', '3', '4', '5', '6', '7', '8'};  // 랭크 (행)

// 좌표 매핑 배열
float map_x[8];
float map_y[8];


// --- 객체 생성 ---
// 1. 서보 드라이버 객체 생성
Adafruit_PWMServoDriver pwm = Adafruit_PWMServoDriver();

// 2. RobotArmIK 객체 생성 (핀 번호 대신 채널 번호와 드라이버 객체의 주소(&pwm)를 전달)
RobotArmIK robotArm(&pwm, SHOULDER_CHANNEL, UPPER_ARM_CHANNEL, LOWER_ARM_CHANNEL, GRIP_CHANNEL, L1, L2, servoMins, servoMaxs);

//x, y 보정 함수
void compensateXY(float &x, float &y, int colIndex, int rowIndex)
{
  // --- X 보정 ---
  float xComp = colIndex * X_COMP_PER_COL;
  xComp = constrain(xComp, 0, X_COMP_MAX);

  x -= xComp;   // 오른쪽 → 안쪽

  // --- Y 보정 ---
  float yComp = rowIndex * Y_COMP_PER_ROW;
  yComp = constrain(yComp, 0, Y_COMP_MAX);

  y -= yComp;     // 멀어질수록 안쪽으로


  // 중앙에서 멀어질수록 보정
  float centerDist = abs(colIndex - 3.5);  // d/e 기준
  float yCenterComp = centerDist * Y_CENTER_COMP_PER_COL;
  yCenterComp = constrain(yCenterComp, 0, Y_CENTER_COMP_MAX);

  y -= yCenterComp;
  
}

// 체스 표기법을 좌표로 변환하는 함수
// Python ML 코드의 좌표계와 일치:
// - (r, c) = (0, 0) → a8 (왼쪽 위)
// - (r, c) = (0, 7) → h8 (오른쪽 위)
// - (r, c) = (7, 0) → a1 (왼쪽 아래)
// - (r, c) = (7, 7) → h1 (오른쪽 아래)
void chessToCoordinates(String chessPos, float &x, float &y, int &colIndexOut, int &rowIndexOut)
{
  colIndexOut = -1;
  rowIndexOut = -1;

  if (chessPos.length() != 2)
  {
    x = 0;
    y = 0;
    return;
  }

  char file = chessPos.charAt(0); // a, b, c, d, e, f, g, h (열, 좌우)
  char rank = chessPos.charAt(1); // 1, 2, 3, 4, 5, 6, 7, 8 (행, 앞뒤)

  // 파일 인덱스 찾기 (a=0, b=1, ..., h=7)
  int fileIndex = -1;
  for (int i = 0; i < 8; i++)
  {
    if (files[i] == file)
    {
      fileIndex = i;
      break;
    }
  }

  // 랭크 인덱스 찾기 (1=0, 2=1, ..., 8=7)
  int rankIndex = -1;
  for (int i = 0; i < 8; i++)
  {
    if (ranks[i] == rank)
    {
      rankIndex = i;
      break;
    }
  }

  if (fileIndex >= 0 && rankIndex >= 0)
  {
    // 기본 좌표 가져오기 (회전 전)
    x = X[fileIndex];  // file a=왼쪽, h=오른쪽
    y = Y[rankIndex];  // rank 1=앞, 8=뒤
    colIndexOut = fileIndex;
    rowIndexOut = rankIndex;
    
    // 체스판 회전 보정 (좌표 레벨 변환)
#if BOARD_ROTATION == 90
    // 시계방향 90도: (x, y) → (y, -x)
    float tempX = x;
    x = y;
    y = -tempX;
#elif BOARD_ROTATION == 180
    // 180도 회전: (x, y) → (-x, -y+2*center_y)
    // 체스판 중심을 기준으로 180도 회전
    float centerY = ROBOT_ARM_OFFSET + BOARD_MARGIN + EFFECTIVE_BOARD_SIZE / 2;
    x = -x;
    y = 2 * centerY - y;
#elif BOARD_ROTATION == 270
    // 반시계방향 90도: (x, y) → (-y, x)
    float tempX = x;
    x = -y;
    y = tempX;
#endif
    
    // 디버깅 출력
    Serial.print("  체스좌표->물리좌표: ");
    Serial.print(chessPos);
    Serial.print(" (File[");
    Serial.print(fileIndex);
    Serial.print("], Rank[");
    Serial.print(rankIndex);
    Serial.print("]) → X=");
    Serial.print(x);
    Serial.print(", Y=");
    Serial.print(y);
    Serial.print(" (회전: ");
    Serial.print(BOARD_ROTATION);
    Serial.println("도)");
  }
  else
  {
    x = 0;
    y = 0;
  }
}

//z 보정 함수
float compensatedZ(float baseZ, int rowIndex) 
{
  float zComp = rowIndex * Z_COMP_PER_ROW;
  zComp = constrain(zComp, 0, Z_COMP_MAX);
  return baseZ + zComp;
}


void setup()
{
  Serial.begin(9600);

  // 서보 드라이버 초기화
  pwm.begin();
  pwm.setOscillatorFrequency(27000000);
  pwm.setPWMFreq(50); // 서보 모터는 50Hz

  // robotArm.begin(); // 라이브러리의 begin()은 현재 비어있으므로 생략 가능
    Serial.println("체스 로봇 좌표 시스템 초기화 완료");

  robotArm.moveTo(365,0,330); // 시작 준비 자세

 
  delay(2000);
}


void loop() {

  if (Serial.available()) {
    // 라즈베리파이(brain)에서 들어오는 명령 처리
    // - 일반 이동: "e2e4" (from→to)
    // - 캡처: "e4cap" (상대 말 제거), 이후 "e2e4" 등
    // - 단일 좌표 테스트: "e2"
    // - 제로 포지션: "zero"

    String pos = Serial.readStringUntil('\n');
    pos.trim();

    bool isCapture    = false;
    bool isZero       = false;
    bool isFullMove   = false;
    bool isGripOpen   = false;
    bool isGripClose  = false;
    String square      = ""; // 단일 좌표 명령용 ("e4", "e4cap"에서 "e4")
    String fromSquare  = ""; // 전체 이동 from ("e2e4" → "e2")
    String toSquare    = ""; // 전체 이동 to   ("e2e4" → "e4")

    // 제로 포지션 명령
    if (pos.equalsIgnoreCase("zero")) {
      isZero = true;
    }
    // 그리퍼 테스트 명령
    else if (pos.equalsIgnoreCase("gripopen")) {
      isGripOpen = true;
    }
    else if (pos.equalsIgnoreCase("gripclose")) {
      isGripClose = true;
    }
    // "e4cap" 같은 형식 감지 (캡처용 단일 좌표 명령)
    else if (pos.endsWith("cap") && pos.length() >= 5) {
      square = pos.substring(0, 2); // 앞 2글자만 체스 좌표
      isCapture = true;
    }
    // 전체 이동 명령 (예: "e2e4")
    else if (pos.length() == 4) {
      fromSquare = pos.substring(0, 2);
      toSquare   = pos.substring(2, 4);
      isFullMove = true;
    }
    // 일반 체스 좌표 (예: "e2")
    else if (pos.length() == 2) {
      square = pos;
    }

    if (isZero) {
      // 제로 포지션으로 복귀
      Serial.println("ZERO 명령 수신: 제로 포지션으로 이동");
      robotArm.moveTo(365, 0, 330); // setup에서 사용한 준비 자세와 동일
      delay(1000);
    }
    else if (isGripOpen) {
      Serial.println("GRIPOPEN 명령 수신: 그리퍼 열기");
      robotArm.gripOpen();
      delay(300);
    }
    else if (isGripClose) {
      Serial.println("GRIPCLOSE 명령 수신: 그리퍼 닫기");
      robotArm.gripClose();
      delay(300);
    }
    // 전체 이동 명령 처리 ("e2e4"처럼 from → to)
    else if (isFullMove && fromSquare.length() == 2 && toSquare.length() == 2) {
      float fx, fy, tx, ty;
      int fCol, fRow, tCol, tRow;
      chessToCoordinates(fromSquare, fx, fy, fCol, fRow);
      chessToCoordinates(toSquare,   tx, ty, tCol, tRow);

      //XY 보정
      compensateXY(fx, fy, fCol, fRow);
      compensateXY(tx, ty, tCol, tRow);
      
      //Z 보정
      float fZ_pick  = compensatedZ(Z_HEIGHT, fRow);
      float fZ_open  = compensatedZ(GRIP_OPEN_HEIGHT, fRow);
      float tZ_place = compensatedZ(Z_HEIGHT, tRow);

      Serial.print("입력(전체 이동): "); Serial.println(pos);
      Serial.print("FROM -> x: "); Serial.print(fx);
      Serial.print(", y: "); Serial.println(fy);
      Serial.print("TO   -> x: "); Serial.print(tx);
      Serial.print(", y: "); Serial.println(ty);

      // 1) 출발 칸으로 이동해서 말을 집기
      robotArm.moveTo(fx, fy, fZ_open);
      delay(400);
      robotArm.gripOpen();
      delay(400);
      robotArm.moveTo(fx, fy, fZ_pick);
      delay(400);
      robotArm.gripClose();
      delay(400);

      // 2) 도착 칸으로 이동해서 말을 놓기
      robotArm.moveTo(tx, ty, tZ_place);
      delay(400);
      robotArm.gripOpen();
      Serial.println("movecomplete");
    }
    // 단일 좌표 명령 처리 ("e4" 또는 "e4cap")
    else if (square.length() == 2) {
      float x, y;
      int colIdx, rowIdx;

      chessToCoordinates(square, x, y, colIdx, rowIdx);
      compensateXY(x, y, colIdx, rowIdx);

      float zPick = compensatedZ(Z_HEIGHT, rowIdx);
      float zOpen = compensatedZ(GRIP_OPEN_HEIGHT, rowIdx);

      Serial.print("입력: "); Serial.println(pos);
      Serial.print("계산된 좌표 -> x: "); Serial.print(x);
      Serial.print(", y: "); Serial.println(y);

      if (isCapture) {
        // 1) 잡을 말 위치로 이동해서 집기
        robotArm.moveTo(x, y, zOpen);
        delay(400);
        robotArm.gripOpen();
        delay(400);
        robotArm.moveTo(x, y, zPick);
        delay(400);
        robotArm.gripClose();
        delay(400);

        // 2) DEAD_ZONE으로 이동해서 버리기
        robotArm.moveTo(380, DEAD_ZONE, Z_HEIGHT);
        delay(400);
        robotArm.gripOpen(); 
        Serial.println("movecomplete");

      } else {
        // 단일 위치 테스트: 해당 위치로 이동 후 집었다가 놓기
        robotArm.moveTo(x, y, zPick);
        delay(1000);
        robotArm.gripClose(); delay(1000);
        robotArm.gripOpen();  delay(1000);
        Serial.println("movecomplete");
      }
    }
  delay(500);  // 입력 템포 조절
  }
}
