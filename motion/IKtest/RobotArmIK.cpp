#include "RobotArmIK.h"
#include <Wire.h>
#include <Adafruit_PWMServoDriver.h>

#define NUM_SERVOS 4  // 사용하는 모터 개수 (어깨, 상박, 하박, 그리퍼)

// 생성자: 드라이버 객체 포인터와 각 서보의 '채널 번호'와 서보별 min/max 값을 입력받습니다.
RobotArmIK::RobotArmIK(Adafruit_PWMServoDriver* pwm,
                     uint8_t channel_shoulder, uint8_t channel_upper,
                     uint8_t channel_lower, uint8_t channel_grip,
                     float L1, float L2,
                     int servoMins[NUM_SERVOS], int servoMaxs[NUM_SERVOS]) {
  this->pwm = pwm;
  this->channel_shoulder = channel_shoulder;
  this->channel_upper = channel_upper;
  this->channel_lower = channel_lower;
  this->channel_grip = channel_grip;
  this->L1 = L1;
  this->L2 = L2;

   // [추가] 배열 복사
  for (int i = 0; i < NUM_SERVOS; i++) {
    this->servoMins[i] = servoMins[i];
    this->servoMaxs[i] = servoMaxs[i];
  }
}



  

// 이 클래스에서는 특별히 할 일이 없으므로 비워둡니다.
// 드라이버 초기화는 메인 .ino 파일에서 수행합니다.
void RobotArmIK::begin() {
  // pwm->begin(); // 드라이버 시작 코드는 setup()에서 한 번만 호출하는 것이 좋습니다.
}




// 각도를 펄스로 변환 (채널별 개별 min/max 적용)
float RobotArmIK::angleToPulse(uint8_t channel, float angle) {
  int idx = 0;

  // 채널 번호에 따라 배열 인덱스 결정
  if (channel == channel_shoulder) idx = 0;
  else if (channel == channel_upper) idx = 1;
  else if (channel == channel_lower) idx = 2;
  else if (channel == channel_grip) idx = 3;

  return map(angle, 0, 180, servoMins[idx], servoMaxs[idx]);
}

void RobotArmIK::moveTo(float x, float y, float z) {
  float theta_shoulder_rad = atan2(y, x);
  float d = sqrt(pow(x, 2) + pow(y, 2));

  float distance = sqrt(pow(d, 2) + pow(z, 2));
  if (distance > L1 + L2) {
    Serial.println("도달할 수 없는 거리");
    return;
  }

  float cos_theta2 = (pow(L1, 2) + pow(L2, 2) - pow(d, 2) - pow(z, 2)) / (2 * L1 * L2);
  float theta_lower_rad = acos(cos_theta2);

  float k1 = L1 + L2 * cos(M_PI-theta_lower_rad);
  float k2 = L2 * sin(M_PI-theta_lower_rad);
  float theta_upper_rad = M_PI - atan2(z, d) - atan2(k2, k1); // theta 값 연산 실수 수정

  float theta_shoulder_deg = theta_shoulder_rad * 180.0 / M_PI;
  float theta_upper_deg    = theta_upper_rad    * 180.0 / M_PI;
  float theta_lower_deg    = theta_lower_rad    * 180.0 / M_PI;

  // [중요] 실제 기구 한계에 맞춰 각도 범위를 조금 좁게 제한
  float shoulder_angle = constrain(theta_shoulder_deg,  0, 180);
  float upper_angle    = constrain(theta_upper_deg,    0, 180);
  float lower_angle    = constrain(theta_lower_deg,    0, 180);

  // ---- 부드러운 모션을 위한 보간 ----
  // 이전 각도에서 목표 각도로 여러 단계에 걸쳐 이동
  static float cur_shoulder = shoulder_angle;
  static float cur_upper    = upper_angle;
  static float cur_lower    = lower_angle;

  const int   STEPS      = 25;   // 단계 수 (값을 늘리면 더 느리고 부드럽게)
  const int   STEP_DELAY = 25;   // 각 단계 사이 지연(ms)

  int pwm_upper_test;

  for (int i = 1; i <= STEPS; i++) {
    float t = (float)i / (float)STEPS;

    float step_shoulder = cur_shoulder + (shoulder_angle - cur_shoulder) * t;
    float step_upper    = cur_upper    + (upper_angle    - cur_upper)    * t;
    float step_lower    = cur_lower    + (lower_angle    - cur_lower)    * t;

    int pwm_shoulder = angleToPulse(channel_shoulder, step_shoulder);
    int pwm_upper    = angleToPulse(channel_upper,    step_upper);
    int pwm_lower    = angleToPulse(channel_lower,    step_lower);
    
    // 디버깅용 로그 (너무 많으면 필요할 때만 사용)
    // Serial.print("[step] sh:"); Serial.print(step_shoulder);
    // Serial.print(" up:"); Serial.print(step_upper);
    // Serial.print(" lo:"); Serial.println(step_lower);

    pwm->setPWM(channel_shoulder, 0, pwm_shoulder);
    pwm->setPWM(channel_upper,    0, pwm_upper);
    pwm->setPWM(channel_lower,    0, pwm_lower);
    int pwm_upper_test = pwm_upper;
    delay(STEP_DELAY);
  }

  // 최종 각도를 현재 상태로 저장
  cur_shoulder = shoulder_angle;
  cur_upper    = upper_angle;
  cur_lower    = lower_angle;

  // 최종 위치 로그
  Serial.print("[moveTo] x: "); Serial.print(x);
  Serial.print(", y: "); Serial.print(y);
  Serial.print(", z: "); Serial.print(z);
  Serial.print(" | shoulder_angle: "); Serial.print(shoulder_angle);
  Serial.print(", upper_angle: "); Serial.print(upper_angle);
  Serial.print(", lower_angle: "); Serial.print(lower_angle);
  Serial.println(pwm_upper_test);
}

// 그리퍼 열기
void RobotArmIK::gripOpen() {
  pwm->setPWM(channel_grip,  0, angleToPulse(channel_grip, 210));   // 0도에 해당하는 펄스 값
}

// 그리퍼 닫기
void RobotArmIK::gripClose() {
  pwm->setPWM(channel_grip,  0, angleToPulse(channel_grip, 210));  // 90도에 해당하는 펄스 값
}
